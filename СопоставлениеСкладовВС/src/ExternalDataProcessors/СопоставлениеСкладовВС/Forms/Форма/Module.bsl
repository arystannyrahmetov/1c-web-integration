#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Объект.РегистОстатков = "РегистрБухгалтерии";	
	ЗаполнитьТабличнуюЧастьСоответствиеСкладов("СоответствиеСкладовИВиртуальныхСкладов");
КонецПроцедуры 
 
&НаКлиенте
Процедура ЗаполнитьСклады(Команда)
	ЗаполнитьТабличнуюЧастьСоответствиеСкладов(Объект.РегистОстатков);	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьРегистрСоответствий(Команда)
	ОчиститьРегистрСоответствийВызов();
	ОчиститьТаблицуСоответствий();
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьНаСервереВызов();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервереВызов()
	ЗаписатьТабличнуюЧастьВРегистрСоответствий(Объект.СопоставлениеСкладов.Выгрузить());
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеТабличнойЧастиСоответствий

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьСоответствиеСкладов(РегистОстатков)
	
	Если РегистОстатков = "РегистрБухгалтерии" Тогда
		Результат = ПолучитьСкладыИПодразделенияПоБУ(ТекущаяДата(), Объект.ВиртуальныйСклад);
	ИначеЕсли РегистОстатков = "ТоварыОрганизацийБУ" Тогда
		Результат = ПолучитьСкладыИПодразделенияПоРегиструНакопления(ТекущаяДата(), Объект.ВиртуальныйСклад);
	ИначеЕсли РегистОстатков = "СоответствиеСкладовИВиртуальныхСкладов" Тогда
		Результат = ПолучитьСоответствиеСкладов();
	КонецЕсли;
	
	Объект.СопоставлениеСкладов.Очистить();
	Объект.СопоставлениеСкладов.Загрузить(Результат);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСкладыИПодразделенияПоБУ(Дата, ВиртуальныйСклад)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТиповойОстатки.СтруктурноеПодразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА ТиповойОстатки.Организация
	|		ИНАЧЕ ТиповойОстатки.СтруктурноеПодразделение
	|	КОНЕЦ КАК Организация,
	|	ТиповойОстатки.Субконто2 КАК Склад,
	| 	&ВиртуальныйСклад КАК ВиртуальныйСклад
	|ИЗ
	|	РегистрБухгалтерии.Типовой.Остатки(
	|			&Дата,
	|			Счет В ИЕРАРХИИ (&СчетТоваров)
	|				ИЛИ Счет В (&ДругиеСчета),
	|			,
	|			) КАК ТиповойОстатки");

	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ВиртуальныйСклад", ВиртуальныйСклад);
	Запрос.УстановитьПараметр("СчетТоваров", ПланыСчетов.Типовой.Запасы); //1300

	ДругиеСчета = Новый СписокЗначений;
	ДругиеСчета.Добавить(ПланыСчетов.Типовой.МонтажОборудования); //2832
	ДругиеСчета.Добавить(ПланыСчетов.Типовой.ПроизводствоИзДавальческогоСырья); //8120
	
	Запрос.УстановитьПараметр("ДругиеСчета", ДругиеСчета);

	Возврат Запрос.Выполнить().Выгрузить();
		
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСкладыИПодразделенияПоРегиструНакопления(Дата, ВиртуальныйСклад)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТоварыОрганизацийБУОстатки.СтруктурноеПодразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА ТоварыОрганизацийБУОстатки.Организация
	|		ИНАЧЕ ТоварыОрганизацийБУОстатки.СтруктурноеПодразделение
	|	КОНЕЦ КАК Организация,
	|	ТоварыОрганизацийБУОстатки.Склад КАК Склад,
	|	&ВиртуальныйСклад КАК ВиртуальныйСклад
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизацийБУ.Остатки(&Дата, ) КАК ТоварыОрганизацийБУОстатки");

	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ВиртуальныйСклад", ВиртуальныйСклад);

	Возврат Запрос.Выполнить().Выгрузить();
		
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСоответствиеСкладов()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СоответствиеСкладовИВиртуальныхСкладов.Склад КАК Склад,
	|	СоответствиеСкладовИВиртуальныхСкладов.Организация КАК Организация,
	|	СоответствиеСкладовИВиртуальныхСкладов.ВиртуальныйСклад КАК ВиртуальныйСклад
	|ИЗ
	|	РегистрСведений.СоответствиеСкладовИВиртуальныхСкладов КАК СоответствиеСкладовИВиртуальныхСкладов");
	
	Возврат Запрос.Выполнить().Выгрузить();
		
КонецФункции

&НаСервере
Процедура ОчиститьТаблицуСоответствий()
	Объект.СопоставлениеСкладов.Очистить();
КонецПроцедуры

#КонецОбласти

#Область ЗаписьВРегистрСоответсвий

&НаСервереБезКонтекста
Процедура ОчиститьРегистрСоответствийВызов()	
	
	НаборЗаписей = РегистрыСведений.СоответствиеСкладовИВиртуальныхСкладов.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьТабличнуюЧастьВРегистрСоответствий(СопоставлениеСкладов)
	
	НаборЗаписей = РегистрыСведений.СоответствиеСкладовИВиртуальныхСкладов.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(СопоставлениеСкладов);
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

#КонецОбласти